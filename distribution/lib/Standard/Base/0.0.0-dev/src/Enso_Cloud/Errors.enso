import project.Any.Any
import project.Data.Text.Text
import project.Network.HTTP.HTTP_Status_Code.HTTP_Status_Code

## PRIVATE
   Error if the user is not logged into Enso Cloud.
type Not_Logged_In
    ## PRIVATE
    Error

    ## PRIVATE
    to_display_text : Text
    to_display_text self = "Not logged into Enso cloud. Please log in and restart."

## PRIVATE
   Indicates that the session with Enso Cloud has expired and the user needs to log in again.
type Cloud_Session_Expired
    ## PRIVATE
       The `underlying_error` is not shown in the error message, but is saved for debugging purposes.
    Error underlying_error

    to_display_text : Text
    to_display_text self = "Login session with Enso Cloud has expired. Please log in again and restart."

## PRIVATE
type Enso_Cloud_Error
    ## PRIVATE
       An unhandled error response from Enso Cloud.
    Unexpected_Service_Error (status_code : HTTP_Status_Code) (payload : Text)

    ## PRIVATE
       An error response from Enso Cloud that could not be parsed.
    Invalid_Response_Payload (cause : Text | Any)

    ## PRIVATE
       An error response from Enso Cloud indicating that the provided user data is invalid.
    Unauthorized

    ## PRIVATE
       An error indicating issues with connecting to Enso Cloud.
    Connection_Error (cause : Any)

    ## PRIVATE
    to_display_text : Text
    to_display_text self = case self of
        Enso_Cloud_Error.Unexpected_Service_Error status_code payload -> "Unexpected error response from Enso Cloud: " + status_code.to_text + ": " + payload
        Enso_Cloud_Error.Invalid_Response_Payload cause -> "Internal error: A response from Enso Cloud could not be parsed: " + cause.to_display_text
        Enso_Cloud_Error.Unauthorized -> "Enso Cloud credentials file was found, but the service responded with 401 Unauthorized. You may try logging in again and restarting the workflow."
        Enso_Cloud_Error.Connection_Error cause -> "Error connecting to Enso Cloud: " + cause.to_display_text

## PRIVATE
type Missing_Data_Link_Library
    ## PRIVATE
    Error (library_name : Text) (data_link_type : Text)

    ## PRIVATE
    to_display_text : Text
    to_display_text self =
        "The data link for "+self.data_link_type+" is provided by the library "+self.library_name+" which is not loaded. Please import the library, and if necessary, restart the project."
