from Standard.Base import all
import Standard.Base.System.File_Format.Auto_Detect
from Standard.Base.Enso_Cloud.Data_Link import parse_format
from Standard.Base.Enso_Cloud.Public_Utils import get_optional_field, get_required_field

import project.AWS_Credential.AWS_Credential
import project.S3.S3_File.S3_File
from project.Internal.Data_Link_Helpers import decode_aws_credential

## PRIVATE
type S3_Data_Link
    ## PRIVATE
    Value (uri : Text) default_format (credentials : AWS_Credential)

    ## PRIVATE
    parse json -> S3_Data_Link =
        uri = get_required_field "uri" json expected_type=Text
        auth = decode_aws_credential (get_required_field "auth" json)
        format = parse_format (get_optional_field "format" json)
        S3_Data_Link.Value uri format auth

    ## PRIVATE
    as_file self -> S3_File = S3_File.new self.uri self.credentials

    ## PRIVATE
    read self (format = Auto_Detect) (on_problems : Problem_Behavior) =
        effective_format = if format != Auto_Detect then format else self.default_format
        self.as_file.read effective_format on_problems

    ## PRIVATE
    with_input_stream self (open_options : Vector) (action : Input_Stream -> Any) -> Any =
        self.as_file.with_input_stream open_options action
